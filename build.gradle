plugins {
    id 'java-library'
    id "maven-publish"
    id 'com.netflix.nebula.rpm' version '11.6.0'
}


version = '1.3.4' 
group = 'ch.psi'
ext.timestamp = new Date().format("yyMMddHHmm")

java {
    sourceCompatibility = '1.11'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


if (!hasProperty('mainClass')) {
    ext.mainClass = 'ch.psi.jstriptool.App'
}
ext.main_class = ext.mainClass

if (!hasProperty('runArgs')) {
    ext.arguments = ''
} else {
    ext.arguments = runArgs.split (" ")
} 

task(debug, dependsOn: 'classes', type: JavaExec) {      
    mainClass = main_class
    classpath = sourceSets.main.runtimeClasspath
    args arguments
    ignoreExitValue = true
    debug true
}

task(run, dependsOn: 'classes', type: JavaExec) {      
    mainClass = main_class
    classpath = sourceSets.main.runtimeClasspath
    args arguments 
    ignoreExitValue = true
    debug false
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task fatJar(type: Jar, dependsOn: jar) {
    archiveClassifier = 'fat'
    duplicatesStrategy = 'exclude'  
    manifest {
        attributes (
            'Main-Class': mainClass,
            'Implementation-Version': version,
            'Build-Time': timestamp,
            //,'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
        )
    }    
    doFirst {
        from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } } 
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'     
        
    with jar
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.epics:ca:1.3.2'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.jfree:jfreechart:1.0.19'
    implementation 'com.formdev:flatlaf:1.6'
    testImplementation group: 'junit', name: 'junit', version: '4.10'
}

def app_name = project.name
def app_path = '/opt/' + project.name + "/" + version
def jarFileName = fatJar.archiveFileName.get()
def jarFilePath = "${app_path}/lib/${jarFileName}"
def sys_bin ='/usr/local/bin/'
def sys_lib ='/usr/local/lib/'

task generateRpm(type: Rpm, dependsOn: [fatJar,sourcesJar]) {
        doFirst {
            def rpmFile = archiveFile.get().asFile
            if (rpmFile.exists()) {
                //Workaround to nebula plugin bug: must first remove old RPMs otherwise the payload digest is corrupted.
                rpmFile.delete()
            }
        }            
                
    packageName project.name
    requires 'java-11-openjdk'
    release = 1
    //version '1.0.0'    
    arch 'noarch'
    os LINUX

    // Metadata for "rpm -qi"    
    summary = 'A Java port of EPICS Strip Tool'
    url = 'https://github.com/paulscherrerinstitute/jstriptool'
    vendor = 'Paul Scherrer Institute' 
    license = 'GPL'
    packageGroup = 'Applications'
    packageDescription = 'This tool is used for plotting time series data from EPICS control systems.'
  
    user='root'
    permissionGroup 'root'
    addParentDirs false

    directory(app_path, 755)
    directory(app_path + '/lib', 755)
    directory(app_path + '/bin', 755)
    
    into app_path  

    from ('build/libs') {
        into 'lib'
        //include fatJar.archiveFileName
        fileMode 0644
    }

    from('scripts') {
        into 'bin'
        //Add version to startup script name
        rename { String fileName ->
            fileName == "startup" ? "${app_name}_${version}" : fileName
        }
        expand([
            "app_name": app_name,
            "app_version": version,
            "jar_file": jarFilePath
        ])
        fileMode 0755
    }

    link(sys_bin + app_name + "_" + version, app_path + '/bin/' + app_name + "_" + version)   
    link(sys_bin + app_name, sys_bin + app_name + "_" + version)   
}

publishing {
    repositories {
        maven {
            credentials {
                username artifactoryUser 
                password artifactoryPwd
            }
            url = artifactoryUrlRel
        }
    }

    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            //artifact javadocJar
            artifact generateRpm
            artifact fatJar
            groupId group
            artifactId project.name
            version version
        }
    }
}


